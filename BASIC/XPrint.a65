
#if BLD == BLD_CODE
;=================================================================== PRINT ===========
;*  X_Print             code to execute the Print
;*
.list
#ifdef hide
.NOLIST
#endif

_Print 		= $

#define NONL() RMB2 ZPrFlg
#define DONL() SMB2 ZPrFlg
#define BR_NNL(tgt) BBR2 ZPrFlg,tgt


;----------------------------------------------- X_Print
X_Print: ; code to execute the Print
 		jsr 	GetChr 		; get the char
 		DONL()
X_PrL 		lda 	CHRGOT
 		beq 	X_PrEnd 	; null->end of print statement

 		;-- TSTR|TNUM  A=CHRGOT
 		BR_AEQ(T_String,X_Pr1)
 		BR_ANE(T_Number,X_Pr2)

X_Pr1 		;-- handle TStr / TNum and loop back
 		DONL() 			; default is NL afterwards
 		jsr 	EvalStr
 		bra 	X_PrL

X_Pr2 		; non-token so should be ',' or ';'
 		BR_AEQ($3B,X_Pr3) 	; ';'$3B
 		BR_ANE($2C,X_Pr4) 	; ','$2C
 		lda 	#HT 		; tab
 		jsr 	PutChr

X_Pr3		NONL()	; don't do the3 NL
X_Pr4		jsr 	GetChr		; comsume , ; ??
 		bra 	X_PrL 		; loop for next part


; end of print statement so output the buffer result
X_PrEnd 	;-- output the result
 		jsr 	StrOut
 		;-- end off with Cr LF if needed
 		BR_NNL(X_PrX) 		; no cr,lf -> exit
 		jmp 	PrNL 		; output CR,LF and exit
X_PrX 		rts



.echo "  _Print\tuses "
.echo $-_Print
.echo " Bytes\n"
.nolist
#endif


;----------------------------------------------- keyword
#if BLD = BLD_KEYWORD
.list
W_Print 	.DB 	"PRIN",'T'+$80
.nolist
#endif
;----------------------------------------------- run
#if BLD = BLD_TBL_RUN
.list
T_Print 	= LOW($)
.DW 	X_Print
.nolist
.export T_Print
#endif


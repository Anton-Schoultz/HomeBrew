
#if BLD == BLD_CODE
;=================================================================== PRINT ===========
;*  X_Print             code to execute the Print
;*
.list
#ifdef hide
.NOLIST
#endif

_Print 		= $

#define NONL() RMB2 ZPrFlg
#define DONL() SMB2 ZPrFlg
#define BR_NNL(tgt) BBR2 ZPrFlg,tgt


;----------------------------------------------- X_Print
X_Print: ; code to execute the Print
 		jsr 	StrClr 		; clear work string
 		;--
X_PrL 		DONL() 			; default is NL afterwards
X_PrL0 		jsr 	GetChr 		; get the char after PRINT

X_PrL1 		beq 	X_PrEnd 	; null=>done

 		BR_AEQ($3B,X_PrSemi) 	; ';' =>Semi
 		BR_AEQ($2C,X_PrComma) 	; ',' =>Colon

 		jsr 	EvalStrL	; entry skips clearing
 		DONL() 			; default is NL

 		lda 	CHRGOT		; CHRGOT holds what ended eval
 		BRA 	X_PrL1

X_PrComma 	;-- comma, do a tab, no CRLF
 		lda 	#HT 		; tab
 		jsr 	StrAddChr
 		;-- fall through to turn off NL, and loop back
X_PrSemi 	;-- Semicolon, no CR/LF
 		NONL()
 		bra 	X_PrL0

; end of print statement so output the buffer result
X_PrEnd 	;-- output the result
 		jsr 	StrOut
 		;-- end off with Cr LF if needed
 		BR_NNL(X_PrX) 		; no cr,lf -> exit
 		jmp 	PrNL 		; output CR,LF and exit
X_PrX 		rts

.export X_Print

.echo "  _Print\t"
.echo $-_Print
.echo "\n"
.nolist
#endif


;----------------------------------------------- keyword
#if BLD = BLD_KEYWORD
.list
W_Print 	.DB 	"PRIN",'T'+$80
.nolist
#endif

;----------------------------------------------- run
#if BLD = BLD_TBL_RUN
.list
T_Print 	= LOW($)
.DW 	X_Print
.nolist
.export T_Print
#endif


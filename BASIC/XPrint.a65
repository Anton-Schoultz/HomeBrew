.list
#ifdef hide
.NOLIST
#endif

#if (($/256)>=PG_ROM) & (($/256)<PG_KEY)
;=================================================================== PRINT =========== 
;*  X_PrNum              evaluate numeric expresion, convert & print
;*  X_PrStr              evaluate a string expression & print it
;*  X_Print              code to execute the Print
;*

_Print 		= $

#define NONL() RMB2 ZPrFlg
#define DONL() SMB2 ZPrFlg
#define BR_NNL(tgt) BBR2 ZPrFlg,tgt


;----------------------------------------------- X_Print 
X_Print: ; code to execute the Print
 		jsr 	ChrRd 		; consume PRINT
 		DONL() 			; default end with CRLF
X_PrintL 	lda 	CHRGOT
 		beq 	X_PrEnd 	; null = done
 		jsr 	X_PrPart 	; (*1) Expand
 		bra 	X_PrintL
X_PrEnd 	;- end so new line?
 		;-- end off with Cr LF if needed
 		BR_NNL(X_PrX) 		; no cr,lf -> exit
 		jmp 	PrNL 		; output CR,LF and exit
X_PrX 		rts

;  -- ',' or ';' control output spacing / NL
X_PrComma 	;-- comma, do a tab, no CRLF
 		lda 	#HT 		; tab
 		jsr 	ChrOut 		; output the tab
 		;-- fall through to turn off NL, and loop back
X_PrSemi 	;-- Semicolon, no CR/LF
 		NONL()
 		jsr 	ChrRd 		; comsume ,/;
 		;-- fall through to X_PrPart

; handle parts inside   (part | , | ;)*

X_PrPart 	;-- first check for comma / semi-colon
 		lda 	CHRGOT
 		beq 	X_PrEnd 	; end of parts
 		BR_AEQ($3B,X_PrSemi) 	; ';' =>Semi
 		BR_AEQ($2C,X_PrComma) 	; ',' =>Colon
 		;-- is it a string?
 		jsr 	EvIsStr
 		bne 	X_PrNum

X_PrStr: ; evaluate a string expression & print it
 		jsr 	EvString
 		jsr 	StrOut
 		DONL()
 		RTN_T() 		; OK

X_PrNum: ; evaluate numeric expresion, convert & print
 		jsr 	EvNumeric
 		;-- convert to string
 		jsr 	NumCvt 		; to string
 		jsr 	NumFmt 		; handle leading zeroes
 		;-- print the string
 		LDAX(NUM_BUF)
 		jsr 	PrStrAX
 		DONL()
 		;-- done
 		RTN_T() 		; OK



.echo "  _PRINT\t"
.echo $-_Print
.echo "\n"
.nolist
#endif


;----------------------------------------------- keyword 
#if ($/256) = PG_KEY
.list
W_Print 	.DB 	"PRIN",'T'+$80
.nolist
#endif

;----------------------------------------------- run 
#if ($/256) = PG_RUN
.list
T_Print 	= LOW($)+$80
.DW 	X_Print
.nolist
 		;-- .export T_Print
#endif
.list 					; end of PRINT

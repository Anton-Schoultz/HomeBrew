#ifdef hide
.NOLIST
#endif
#if (($/256)>=PG_ROM) & (($/256)<PG_KEY)

#define ERROR(n) ldx #n\ jmp Error


;=================================================================== ERROR ============
; NOTE: Message nos go up in 2s
;
;*  Error                Hanlde error[X] resetting to warm start
;*  Msg                  Display Message (ZPtr)[X] A @/! Y #/$ Char/hex
;*  RegLoad              Load registers from ZReg~
;*  RegSave              Save registers into ZReg~
;*
_Err 		= $

;----------------------------------------------- X_Err
Error: ; Hanlde error[X] resetting to warm start
 		pha
 		ZSET(ZPtr,ErrTbl) 	; point to the keyword table
 		jsr 	PrNL
 		pla
 		;-- Display Message (ZPtr)[X] A:@/! Y:#/% Char/hex
 		jsr 	Msg
 		jmp 	Main

; @	A as character
; !	A as hex
; #	Y as char
; $	Y as hex
; % 	Y as decimal

;----------------------------------------------- RegLoad
RegLoad: ; Load registers from ZReg~
 		lda 	ZRegA 		; load .A
 		ldy 	ZRegY 		; load .Y
 		ldx 	ZRegX 		; load .X
 		rts

;----------------------------------------------- RegSave
RegSave: ; Save registers into ZReg~
 		sta 	ZRegA 		; save .A
 		sty 	ZRegY 		; save .Y
 		stx 	ZRegX 		; save .X
 		rts

#define ZTokPtr ZPtr
;----------------------------------------------- Msg
Msg: ; Display Message (ZPtr)[X] A:@/! Y:#/$ Char/hex
 		jsr 	RegSave 	; save regs
 		ldy 	#0		; in case X=0
 		txa
 		and 	#$7F
 		lsr 	a 		; A = TokNo / 2
 		tax 			; index in table
 		cpx 	#0
 		beq 	MsgL0 		; token 80 is special ;P
 		ldy 	#$FF
 		;--
MsgNxt 		cpx 	#0 		; no more
 		beq 	MsgX
 		;-- move to next word
MsgNxtL 	iny
 		lda 	(ZTokPtr),y
 		beq 	MsgInvalid 	; end of list, not found
 		bpl 	MsgNxtL
 		;-- got the marker
 		dex
 		bne 	MsgNxtL
MsgL 		;-- found the start of the word
 		iny 			; skip over the last end-of-word marker
MsgL0 		;-- output the word
 		lda 	(ZTokPtr),y
 		beq 	MsgX 		; end of list -> done
 		bmi 	MsgLX 		; last char
 		BR_AEQ('@',MsgAC) 	; ZRegA as character
 		BR_AEQ('!',MsgAH) 	; ZRegA as hex
 		BR_AEQ('#',MsgYC) 	; ZRegY as character
 		BR_AEQ('$',MsgYH) 	; ZRegY as hex
 		BR_AEQ('%',MsgYD) 	; ZRegY as hex
 		jsr 	PutChr 		; echo
 		bra 	MsgL 		; loop next char

MsgLX 		;-- last character of the word (marked one)
 		and 	#$7F 		; mask off bit7
 		jsr 	PutChr 		; output it and return
 		jsr 	IsAlpha
 		BR_F(MsgX) 		; ends in alpha?
 		lda 	#' ' 		; yes, pad a blank
 		jsr 	PutChr
MsgX 		;jsr ChrRd
 		RTN_T() 		; otherwise done
MsgInvalid 	; not found
 		RTN_F() 		; return false
MsgAC 		lda 	ZRegA 		; '@' A as char
 		jsr 	PutChr
 		bra 	MsgL
MsgAH 		lda 	ZRegA 		; '!' A as hex
 		bra 	MsgHex

MsgYC 		lda 	ZRegY 		; '#' Y as char
 		jsr 	PutChr
 		bra 	MsgL
MsgYH 		lda 	ZRegY 		; '$' Y as hex
MsgHex 		pha
 		lda 	#'$'
 		jsr 	PutChr
 		pla
 		jsr 	PrAHex
 		bra 	MsgL
MsgYD 		lda 	ZRegY
MsgDec 		jmp 	PrADec3

#define DOT '.'+$80
; @	A as character
; !	A as hex
; #	Y as char
; $	Y as hex
; % 	Y as decimal


; NOTE: Message nos go up in 2s
#define ERR_OK 		0
#define ERR_EXPECTED 	2
#define ERR_CONST 	4
#define ERR_GOSUB 	6
#define ERR_FOR 	8
#define ERR_VAR 	10
#define ERR_BADVAR 	12
#define ERR_DIV0 	14


ErrTbl
.DB "OK",DOT 				; 0=ERR_OK
.DB "Expected ! but got $",DOT 		; 2=ERR_EXPECTED
.DB "Expected constant",DOT 		; 4=ERR_CONST
.DB "GOSUB/RETURN",DOT 			; 6=ERR_GOSUB
.DB "FOR/NEXT",DOT 			; 8=ERR_FOR
.DB "Expected variable",DOT 		;10=ERR_VAR
.DB "Invalid variable",DOT 		;12=ERR_BADVAR
.DB "Divide by zero",DOT 		;14=ERR_DIV0

.DB 0




.echo "  _Error\t"
.echo $-_Err
.echo "\n"
.list
#endif

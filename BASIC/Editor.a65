;=================================================================== EDITOR ===========
;*  EdtAdd               Append TOKBUF line to end of program
;*  EdtClear             Clear variables and arrays
;*  EdtCpy               Copy from TOKBUF->(ZTgt)
;*  EdtDel               Delete line and fall through to Insert
;*  EdtDwn               Move bytes down (Delete)
;*  EdtIns               Insert line, provided it is not an 'empty' line (length <5)
;*  EdtNew               NEW = clear program, variables etc.
;*  EdtOpn               Move memory up
;*  EdtSeek              Search for line number >= LN_FIND
;*  EdtTop               Set LN_NXT and ZRd to start of program, (ZRd)->.A->CHRGOT
;*  EdtUp                Move bytes up (Insert)
;*  EdtUpd               Update program with line in TOKBUF
;*
.LIST
#ifdef hide
.NOLIST
#endif
#if (($/256)>=PG_ROM) & (($/256)<PG_KEY)


;  Int(RAMBEG)	; Start of user ram area (set with cold start)
;  Int(RAMEND)	; End of user ram area (set at cold start)
;  Int(BASBEG)	; Start of BASIC program
;  Int(BASEND)	; end of program, start of variables
;  Int(VAREND)	; end of variables
;  Int(STREND)  ; end of string work area
;  Int(ARYBEG)  ; Start of array Space

; RAMBEG    BASBEG          BASEND      VAREND        STREND  ARYBEG  RAMEND
;   | A-Z Vars | Program lines | Variables | String work | Free | Arrays |  blank | ROM

;-----------------------------------------------
; X_LineNo: ; Get LN_NUM & Set up LN_NXT
; enters with A=CHRGOT=T_LineNo, exit with CHRGOT after the line data
; sets LN_NUM=line number and LN_NXT=address of next line




_Edt 		= $

;----------------------------------------------- EdtNew
EdtNew: ; NEW = clear program, variables etc.
 		;-- Start Basic at start of RAM
 		ZCPY(BASBEG,RAMBEG)
 		ZCPY(ZWr,BASBEG) 	; prepare to write to memory
 		;-- Null terminate Program
 		lda 	#0
 		jsr 	ChrWr 		; Write Character to memory  A -> (ZWr++) , 00->(ZWr)
 		jsr 	ChrWr
 		ZCPY(BASEND,ZWr) 	; Set BASEND
EdtClear: ; Clear variables and arrays
 		ZCPY(ZWr,BASEND) 	; end of code
 		;-- clear variables
 		lda 	#0
 		jsr 	ChrWr
 		jsr 	ChrWr
 		ZCPY(VAREND,ZWr) 	; Set VAREND
 		ZCPY(STREND,ZWr) 	; Set STREND
 		;-- clear arrays
 		ZCPY(ARYBEG,RAMEND)
 		ZDEC(ARYBEG) 		; top the last ram byte
 		lda 	#0
 		sta 	(ARYBEG)
 		RTN_T()

;----------------------------------------------- EdtTop
EdtTop: ; Set LN_NXT and ZRd to start of program, (ZRd)->.A->CHRGOT
 		ZCPY(LN_NXT,BASBEG)
 		ZCPY(ZRd,BASBEG)
 		;-- this fetch for other users (EdtSeek will do it's own)
 		lda 	(ZRd)
 		sta 	CHRGOT
 		rts

;----------------------------------------------- EdtSeek
EdtSeek: ; Search for line number >= LN_FIND
 		jsr 	EdtTop 		; start at the top
EdtSeekL 	jsr 	X_LineNext 	; LN_NXT->ZRd,(ZRd)->.A->CHRGOT
 		beq 	EdtSeekEnd 	; end of program space
 		;-- set LN_NUM=line number, LN_NXT=address of next line
 		jsr 	X_LineNo
 		;-- is it >= the one we're looking for?
EdtTest 	sec
 		lda 	LN_FIND
 		sbc 	LN_NUM
 		sta 	TMP
 		lda 	LN_FIND+1
 		sbc 	LN_NUM+1
 		sta 	TMP+1
 		bmi 	EdtSeekX
 		;-- check for equal
 		ora 	TMP
 		beq 	EdtSeekX
 		;-- Not there yet
 		bra 	EdtSeekL

EdtSeekEnd 	clc 			; Z:1 C:0 N:0	= end

EdtSeekX 	rts


;----------------------------------------------- EdtUpdate
EdtUpd: ; Update program with line in TOKBUF
; LN_FIND has line no from tokbuf
; ZCnt is length of the line in tokbuf
 		jsr 	EdtSeek
 		;-- N:1 = Insert
 		bmi 	EdtIns 		; N:1 NUM>FND -> Insert
 		bcc 	EdtAdd 		; C:0 End of programm
 		;-- Z:1 -> Delete, then Insert if not empty
EdtDel: ; Delete line and fall through to Insert
 		PRINT("DEL\r\n")





EdtIns: ; Insert line, provided it is not an 'empty' line (length <5)
 		lda 	#5
 		cmp 	LN_LEN
 		bpl 	EdtInsX 	; Empty Line -> exit
 		;-- Insert the line
 		PRINT("INS ")
EdtInsX 	rts 			; Done

;----------------------------------------------- EdtAdd
EdtAdd: ; Append TOKBUF line to end of program
 		ZCPY(ZTgt,ZRd) 		; where we read the terminating null
 		ZADD(BASEND,ZCnt) 	; BASEND += Len
;----------------------------------------------- EdtCpy
EdtCpy: ; Copy from TOKBUF->(ZTgt)
 		ZSET(ZSrc,TOKBUF) 	; from the token buffer
 		ldy 	ZCnt
EdtCpyL 	lda 	(ZSrc),y
 		sta 	(ZTgt),y
 		dey
 		bpl 	EdtCpyL
 		rts
.export BASEND

;=================================================================== INS/DEL ==========

;----------------------------------------------- EdtDwn
; Move bytes down (Delete)
; ZSrc = source start address		Changed
; ZTgt = destination start address	Changed
; ZCnt = number of bytes to move	Preserved
EdtDwn: ;Move bytes down (Delete)
 		ldy 	#0
 		ldx 	ZCnt+1
 		beq 	EdtDwn2
EdtDwn1 	lda 	(ZSrc),Y 	; move a page at a time
 		sta 	(ZTgt),Y
 		iny
 		bne 	EdtDwn1
 		inc 	ZSrc+1
 		inc 	ZTgt+1
 		dex
 		bne 	EdtDwn1
EdtDwn2 	ldx 	ZCnt
 		beq 	EdtDwn4
EdtDwn3 	lda 	(ZSrc),Y 	; move the remaining bytes
 		sta 	(ZTgt),Y
 		iny
 		dex
 		bne 	EdtDwn3
EdtDwn4 	rts

;----------------------------------------------- EdtUp
; Move bytes up (Insert)
; ZSrc = source end address		Changed
; ZTgt = destination end address	Changed
; ZCnt = number of bytes to move 	Preserved
EdtUp: ; Move bytes up (Insert)
 		ZINC(ZSrc) 		; adjust source and target
 		ZINC(ZTgt)
 		;--
 		ldy 	#$FF
 		ldx 	ZCnt+1
 		beq 	EdtUp3
EdtUp1 		dec 	ZSrc+1
 		dec 	ZTgt+1
EdtUp2 		lda 	(ZSrc),Y 	; move a page at a time
 		sta 	(ZTgt),Y
 		dey
 		bne 	EdtUp2
 		lda 	(ZSrc),Y 	; handle Y = 0 separately
 		sta 	(ZTgt),Y
 		dey
 		dex
 		bne 	EdtUp1
EdtUp3 		ldx 	ZCnt
 		beq 	EdtUp5
 		dec 	ZSrc+1
 		dec 	ZTgt+1
EdtUp4 		lda 	(ZSrc),Y 	; move the remaining bytes
 		sta 	(ZTgt),Y
 		dey
 		dex
 		bne 	EdtUp4
EdtUp5 		rts

;----------------------------------------------- EdtOpn
; Move memory up (open up a space)
; ZSrc = source start address		Changed
; ZTgt = destination start address	Changed
; ZCnt = number of bytes to move	Preseved
;
EdtOpn: ; Move memory up
 		ldx 	ZCnt+1 		; the last byte must be moved first
 		clc 			; start at the final pages of ZSrc and ZTgt
 		txa
 		adc 	ZSrc+1
 		sta 	ZSrc+1
 		clc
 		txa
 		adc 	ZTgt+1
 		sta 	ZTgt+1
 		inx 			; allows the use of BNE after the DEX below
 		ldy 	ZCnt
 		beq 	EdtOpn3
 		dey 			; move bytes on the last page first
 		beq 	EdtOpn2
EdtOpn1 	lda 	(ZSrc),Y
 		sta 	(ZTgt),Y
 		dey
 		bne 	EdtOpn1
EdtOpn2 	lda 	(ZSrc),Y 	; handle Y = 0 separately
 		sta 	(ZTgt),Y
EdtOpn3 	dey
 		dec 	ZSrc+1 		; move the next page (if any)
 		dec 	ZTgt+1
 		dex
 		bne 	EdtOpn1
 		rts


.echo "  _Edtor\t"
.echo $-_Edt
.echo "\n"
.list
#endif

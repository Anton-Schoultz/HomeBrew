;=================================================================== EVALUATE =========
;*  BoolOr               OR
;*  BoolTerm             Boolean AND
;*  BoolXor              XOR
;*  Check                check for a given token in CHRGOT
;*  DB_EvStr             Debug Eval - ZRd  SnEx NumA Gt Fl  SnEx=S_LEN_A, NumA=S_PTR_A
;*  DoAddOp              Do Add/Sub - rts back to inside while
;*  ENumLit              Fetch number from the code -> NUM_A (unpacked)
;*  EvAdd                add
;*  EvDiv                Do Divide
;*  EvFAry               TODO Retrieve array variable value -> NumA
;*  EvFVar               retrieve variable value -> NumA
;*  EvMod                Do Modulus
;*  EvMul                Do Multiply
;*  EvNumeric            Evaluate numeric expression, return T if valid (and CHRGOT is next char)
;*  EvSub                subtract
;*  EvVarLong            TODO Get value of long name variable
;*  Expression           [ Term ]   ( [+/-] Term)*
;*  Factor               Factor = (BoolTerm) | NumLit | NumVar
;*  Match                match character CHRGOT vs .A and move on, error if no match
;*  NotFactor            Boolean NOT
;*  Relation             Handle relation test < = > <= <> >=
;*  Term                 Factor [ '*'|'/'|'%' Factor]*
;*
#ifdef hide
.NOLIST
#endif

_Ev 		= $

;----------------------------------------------- EvNumeric
EvNumeric: ; Evaluate numeric expression, return T if valid (and CHRGOT is next char)
 		lda 	#0
 		sta 	EVFLG
 		jsr 	BoolTerm

 		BR_T(EvNumL)
 		; no terms found - not numeric expression
 		rts


EvNumL 		jsr 	IsOrOp
 		bne 	EvNumX
 		;--
 		jsr 	NumPushA
 		jsr 	DoOrOp
 		bra 	EvNumL
 		;--
EvNumX 		php
 		jsr 	NumPack
 		plp
 		rts

;----------------------------------------------- DoOrOp
DoOrOp 		lda 	CHRGOT
 		CASE(T_OR,BoolOr)
 		CASE(T_XOR,BoolXor)
 		RTN_F()

;----------------------------------------------- IsOrOp
IsOrOp 		; return EQ if 'OR' / 'XOR'
 		lda 	CHRGOT
 		cmp 	#T_OR
 		beq 	IsOrOpX
 		cmp 	#T_XOR
IsOrOpX 	rts

;----------------------------------------------- BoolOr
BoolOr: ; OR
 		jsr 	ChrRd
 		jsr 	BoolTerm
 		jsr 	NumPopB
 		jsr 	NumOr
 		lda 	CHRGOT
 		jsr 	ChrRd
 		RTN_T()

;----------------------------------------------- BoolXor
BoolXor: ; XOR
 		jsr 	ChrRd
 		jsr 	BoolTerm
 		jsr 	NumXor
 		lda 	CHRGOT
 		RTN_T()

;----------------------------------------------- BoolTerm
BoolTerm: ; Boolean AND
 		jsr 	NotFactor
BoolTermL 	lda 	CHRGOT
 		cmp 	#T_And
 		bne 	BoolTermX
 		jsr 	NumPushA
 		jsr 	ChrRd
 		jsr 	NotFactor
 		jsr 	NumPopB
 		jsr 	NumAnd
 		bra 	BoolTermL
BoolTermX 	RTN_T()

;----------------------------------------------- NotFactor
NotFactor: ; Boolean NOT
 		lda 	CHRGOT
 		cmp 	#T_NOT
 		bne 	Relation
 		;--
 		jsr 	ChrRd 		; consume NOT
 		jsr 	Relation
 		;-- negate realtion result 0<->1
 		jsr 	NumNot
 		lda 	CHRGOT
 		RTN_T()

;----------------------------------------------- Relation
Relation: ; Handle relation test < = > <= <> >=
 		jsr 	Expression
 		jsr 	IsRelOp
 		bne 	RelationX
 		;--
 		lda 	CHRGOT 		; fetch the token again
 		pha 			; save the relop on stack
 		;--
 		jsr 	NumPushA
 		jsr 	ChrRd 		; consume op
 		;--
 		jsr 	Expression 	; get second expression
 		jsr 	NumPopB 	;
 		jsr 	NumSub 		; subtract
 		jsr 	NumFlag 	; set flags bassed on result
 		;-- test condition
 		pla 			; recal the rel-op
 		and 	NUM_FLG 	; and with the flag
 		and 	#$7 		; opcode low bits match NUM_FLG
 		beq 	RelFalse 	; zero is false
 		;-- non-zero means true
 		jsr 	NumTrue 	; TRUE = FFFF
 		RTN_T()
RelFalse 	; set NUMA to zero
 		jsr 	NumFalse 	; FALSE = 0
RelationX 	RTN_T()

; return eq if op is one of  <,=,>,<=,<>,>=
IsRelOp 	and 	#$F8
 		cmp 	#$F0
 		rts

;----------------------------------------------- Expression
Expression: ;  [ Term ]   ( [+/-] Term)*
 		jsr 	IsAddOp
 		bne 	Exp1
 		;-- handle unary as implied zero
 		jsr 	NumZeroA
 		bra 	ExpL
Exp1 		jsr 	Term
 		RTN_IFF() 		; if term is not numeric then exit
 		;-- while isAddOp {
ExpL 		jsr 	IsAddOp
 		bne 	ExpXT
 		jsr 	DoAddOp
 		BR_T(ExpL)
 		;-- }
ExpXT 		RTN_T()

DoAddOp: ; Do Add/Sub - rts back to inside while
 		lda 	CHRGOT
 		CASE('+',EvAdd)
 		CASE('-',EvSub)
 		RTN_F()
EvAdd: ; add
 		jsr 	NumPushA
 		jsr 	ChrRd 		; comsume the '+'
 		jsr 	Term
 		jsr 	NumPopB
 		jsr 	NumAdd
 		RTN_T()

EvSub: ; subtract
 		jsr 	NumPushA
 		jsr 	ChrRd 		; consume the '-'
 		jsr 	Term
 		jsr 	NumPopB
 		jsr 	NumSub
 		RTN_T()

; check if CHRGOT is an add op, returns EQ if true
IsAddOp 	lda 	CHRGOT
 		cmp 	#'+'
 		beq 	IsAddOpX
 		cmp 	#'-'
IsAddOpX 	rts


;----------------------------------------------- Term
Term: ; Factor [ '*'|'/'|'%' Factor]*
 		jsr 	Factor

TermL 		jsr 	IsMulOp 	; while IsMulOP
 		bne 	TermX 		; {
 		jsr 	DoMulOp 	;   DoMulOp
 		BR_T(TermL) 		; }
TermX 		RTN_T()

DoMulOp 	lda 	CHRGOT
 		CASE('*',EvMul)
 		CASE('/',EvDiv)
 		CASE('%',EvMod)
 		RTN_F()

EvMul: ; Do Multiply
 		jsr 	NumPushA
 		jsr 	ChrRd
 		jsr 	Factor
 		jsr 	NumPopB
 		jsr 	NumMul
 		RTN_T()

EvDiv: ; Do Divide
 		jsr 	NumPushA
 		jsr 	ChrRd
 		jsr 	Factor
 		jsr 	NumPopB
 		jsr 	NumDiv
 		RTN_T()

EvMod: ; Do Modulus
 		jsr 	NumPushA
 		jsr 	ChrRd
 		jsr 	Factor
 		jsr 	NumPopB
 		jsr 	NumMod
 		RTN_T()

; Chekc if CHRGOT is '*' or '/', returns EQ if true
IsMulOp 	lda 	CHRGOT
 		cmp 	#'*'
 		beq 	IsMulOpX
 		cmp 	#'/'
 		beq 	IsMulOpX
 		cmp 	#'%'
IsMulOpX 	rts

;----------------------------------------------- Factor
Factor: ; Factor = (BoolTerm) | NumLit | NumVar | Function(~)
; exits with next token in CHRGOT
 		lda 	CHRGOT
 		CASE(T_NumVar,EvFVar) 	; variable
 		CASE(T_NumAry,EvFAry) 	; subscripted var
 		CASE(T_NumLit,ENumLit) 	; Literal value
; 		lda 	CHRGOT
 		jsr 	IsNumFn
 		BR_T(Function)
 		RTN_F()

Function 	;-- process a function
; 		lda 	CHRGOT
 		pha			; save the '(' / T_Fn
 		jsr 	ChrRd 		; consume T_fn
 		jsr 	BoolTerm	; evaluate
 		;-- check for ')'
 		lda 	#$29
 		jsr 	Match
		;-- now execute the function - skip for '('
 		pla
 		cmp 	#'(' 		; braces?
 		beq 	FunctionX 	; yes - done ok
 		;-- jump to perform the function
 		jsr 	RunJump
FunctionX
 		RTN_T()

;----------------------------------------------- ENumLit
ENumLit: ; Fetch number from the code -> NUM_A (unpacked)
; CHRGOT is first char AFTER the stored value
 		;-- fetch number from memory
 		jsr 	NumRd
 		jsr 	ChrRd 		; move on
 		inc 	EVFLG 		; got a value
 		RTN_T()

;----------------------------------------------- EvFVar
EvFVar: ; retrieve variable value -> NumA
; CHRGOT is first char AFTER the variable spec
; VARNUM has XX AABBCC format (packed)
; .A is the index, math module multiplies for size as required
 		jsr 	ChrRd 		; get first letter of name
 		bpl 	EvVarLong 	; get value of long name variable
 		and 	#$1F 		; mask for index
 		jsr 	NumGet 		; retrieve simple variable [A]
 		jsr 	ChrRd 		; get next token into CHRGOT
 		inc 	EVFLG 		; got a value
 		RTN_T() 		; got a value

;----------------------------------------------- EvVarLong
EvVarLong: ; TODO Get value of long name variable
 		ERROR(ERR_BADVAR) 	; invalid variable
 		;-- inc	EVFLG		; got a value
;----------------------------------------------- EvFAry
EvFAry: ; TODO Retrieve array variable value -> NumA
 		ERROR(ERR_BADVAR) 	; invalid variable
 		;-- inc	EVFLG		; got a value

;----------------------------------------------- Check
Check: ; check for a given token in CHRGOT
 		cmp 	CHRGOT
 		bne 	MatchFail
 		rts

;----------------------------------------------- Match
Match: ; match character CHRGOT vs .A and move on, error if no match
 		cmp 	CHRGOT
 		bne 	MatchFail
 		jmp 	ChrRd 		; next token and return
MatchFail 	; expected in A
		;- save caller's return address
		ply
		sty	ERR_ADR
		ply
		sty	ERR_ADR+1
		;
 		ldy 	CHRGOT 		; got in Y
 		ldx 	#ERR_EXPECTED
 		jmp 	Error


#if _DB_EVAL
;=================================================================== DBUG =============
.export NUM_A,NUM_B,NUM_BUF

;----------------------------------------------- DB_EvStr
DB_EvStr: ; Debug Eval - ZRd  SnEx NumA Gt Fl  SnEx=S_LEN_A, NumA=S_PTR_A
 		PUSHALL()
 		jsr 	PrNL
 		PRINTLN("ZRd  SnEx NumA Gt Fl")
 		HEX_INT(ZRd)
 		HEX_INT(NUM_AS)
 		HEX_INT(NUM_AA)
 		HEX_SHORT(CHRGOT)
 		HEX_SHORT(EVFLG)
 		jsr 	PrNL
 		PULLALL()
 		PAUSE()
 		rts
#endif


.echo "  _Eval\t\t"
.echo $-_Ev
.echo "\n"
.list



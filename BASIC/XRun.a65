#ifdef hide
.NOLIST
#endif

#if (($/256)>=PG_ROM) & (($/256)<PG_KEY)
;=================================================================== RUN ==============
;*  RunCmd               Run Cmd from TOKBUF
;*  RunInit              prepare to run by setting flags
;*  RunStmnt             Run a statement
;*  TokErr               display invalid token as [xx]
;*  X_Run                Execute Run
;*
_Run 		= $

RunInit: ; prepare to run by setting flags
 		PR_RD_MEM() 		; read from memory (code space)
 		PR_WR_IO() 		; output to I/O
 		PR_ZSUP_OFF()
		PR_ZSPC_ON()
 		;-- init
 		jsr 	NumInit 	; initialise evaluator
 		;-- default line no zero
 		lda 	#0
 		sta 	LN_FND
 		sta 	LN_FND+1
 		rts

;----------------------------------------------- RunCmd
RunCmd: ; Run Cmd from TOKBUF
 		ZSET(ZRd,TOKBUF-1) 	; where to run from
 		jsr 	RunInit 	; set I/O, Zero suppress etc
 		jmp 	RunStmnt

;----------------------------------------------- X_Run
X_Run: ; Execute Run
 		jsr 	RunInit 	; set I/O, Zero suppress etc
 		jsr 	ChrRd 		; consume RUN
 		BR_ANE(T_NumLit,Run) 	; start at default line no 0
 		;-- numeric literal is start line
 		jsr 	NumRd 		; returns in A=Hi, X=Lo
 		sta 	LN_FND+1
 		stx 	LN_FND
Run 		;-- Run from line LN_FND
 		jsr 	LnSeekTop
 		lda 	CHRGOT
RunL 		beq 	RunX 		; EOF -> exit
 		jsr 	RunStmnt
 		;-- move to next line
RunNxt 		jsr 	LnNext 		; Next line LN_NXT->ZRd->LN_ADR
 		bra 	RunL
RunX 		rts

;----------------------------------------------- RunLn
RunStmnt: ; Run a statement
 		jsr 	ChrRd 		; (++ZRd)->A->CHRGOT
 		sta 	TOKNO
 		beq 	RunLnX 		; null = End-of-statement
 		;-- remark ?
 		BR_AEQ(T_Rem,RunLnX) 	; remark skip rest
 		;-- token
 		BR_AGT(T_Max,TokErr) 	; >max = invalid token
 		BR_ALT($80,TokErr) 	; <$80 = invalid token
 		;-- jump to X_xxxx, rts to run loop
 		and 	#$7F
 		tax
 		jmp 	(TblRun,X)
RunLnX 		rts 			; done, return

;----------------------------------------------- TokErr
TokErr: ; display invalid token as [xx]
 		lda 	#'['
 		jsr 	PutChr
 		lda 	TOKNO
 		jsr 	PrAHex
 		lda 	#']'
 		jsr 	PutChr
 		rts


.echo "  _RUN\t\t"
.echo $-_Run
.echo "\n"
.nolist
#endif

;----------------------------------------------- keyword
#if ($/256) = PG_KEY
.list
W_Run 		.DB 	"RU",'N'+$80
.nolist
#endif
;----------------------------------------------- run
#if ($/256) = PG_RUN
.list
T_Run 		= LOW($)
.DW 	X_Run
.nolist
 		;-- .export T_Run
#endif
.list 					; end of Run

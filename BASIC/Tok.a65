;=================================================================== TOK ==============
;*  BasTop              Setup to read from start of basic, for now TOKBUF
;*  FetchNum            fetch number from the code -> NUMBER -> AX
;*  IsAlpha             returns T/F if .A is a Alpha A-Z
;*  IsAlphaNumeric      returns true if .A is 0-9,A-Z
;*  IsDigit             returns T/F if .A is a digit 0..9
;*  IsRelOp             returns T/F if CHRGOT is a relational opperator <>=
;*  L_LineNo            list LineNo
;*  L_Number            list number
;*  L_String            list out string text
;*  L_Var               list 'simple' variable
;*  L_VarName           list long variable
;*  List                List from start of program
;*  ListLn              List line pointed to by ZRd
;*  NextLine            point ZRd to the next line NXTLN
;*  P_IsToken           Parse for a token, returns TOKNO or CHRGOT, returns found T/F
;*  P_LineNo            Parse line numebr and place int TOKBUF
;*  P_Number            parse a number into token & value
;*  P_Part              parse a part of the line
;*  P_PartNxt           advance and do part
;*  P_String            parse quoted string into token
;*  P_Var               Check for variable name
;*  P_VarName           long variable name
;*  Parse               Parse INBUF into TOKBUF
;*  ParseIn             Parse from memory into TOKBUF
;*  Run                 Run from start
;*  RunJmp              run via jump table, return to (*2)
;*  RunLn               Run line pointed to by ZRd
;*  RunTok              run token
;*  SkipBlank           Skip blank/tabs, end char in A, T if not null
;*  TokExpand           Expand the token to key word (X is index)
;*  TokFind             try to find a matching token
;*  X_LineNo            Get LNNUM & Set up NXTLN
;*
#ifdef hide
.NOLIST
#endif

PARAGRAPH()
_Tok 		= $

;----------------------------------------------- ChkList
;	ChkList: ; List of characters used by IsOneOf
;	RelOp: ; Relational opperators
;	.DB 	"<>=", 0
;	Digit: ; List of digits 0..9
;	.DB 	"0123456789",0

T_Line 		= $FF 			; header includes line number
T_Number 	= $FE 			; numeric value
T_String 	= $FD 			; string of characters
T_Var 		= $FC 			; simple variable (single char)
T_VarName 	= $FB 			; long varname

BAS_EOL 	= $00 			; end of line
BAS_PRG 	= $00 			; end of program

#define TOKLEN (TOKBUF+3)

;----------------------------------------------- Parse
Parse: ;Parse INBUF into TOKBUF
 		;-- read from memory INBUF
 		ZSET(ZRd,INBUF-1) 	; read memory from INBUF
;----------------------------------------------- ParseIn
ParseIn: ;Parse from memory into TOKBUF
 		PR_RD_MEM() 		; read from memory (ZRd)
 		PR_ECHO_OFF() 		; don't echo it
 		;-- write to TOKBUF
 		ZSET(ZWr,TOKBUF) 	; point to TOKBUF
 		PR_WR_MEM() 		; write to memory
 		jsr 	ParseClr 	; clear TOKBUF
 		;-- prime pump -> CHRGOT
 		jsr 	GetChr
 		;-- line number ?
 		jsr 	SkipBlank 	; skipover leading blank/tabs
 		jsr 	IsDigit
 		BR_F(P_Stmnt) 		; not a digit -> Statement?
 		;-- digit - so do line number
 		jsr 	P_LineNo
 		lda 	CHRGOT

P_Stmnt 	jsr 	P_Part 		; parse parts until end of line


 		;-- end of input line
 		lda 	#BAS_EOL 	; end-of-line marker
 		jsr 	ChrWr

 		;-- check for T_LnNo
 		lda 	TOKBUF
 		BR_ANE(T_Line,P_StmntX)
 		;-- line no, so get line length
 		sec
 		lda 	ZWr
 		sbc 	#LOW(TOKLEN)
 		;-- fill it in
 		sta 	TOKLEN
 		;--
P_StmntX 	rts 			; done with the whole line

P_PartNxt: ; advance and do part
 		jsr 	GetChr
P_Part: ; parse a part of the line
 		jsr 	SkipBlank 	; skipover leading blank/tabs
 		BR_T(P_KeyWord) 	; Not-null -> P_KeyWord
 		rts 			; null end
 		;-- check for keyword
P_KeyWord:
 		jsr 	P_IsToken 	; check if a keyword
 		BR_F(PNotKey) 		; no -> PNotKey, A=CHRGOT
 		;-- have a token in A, place it and continue
 		jsr 	ChrWr
 		bra 	P_PartNxt 	; loop for more

PNotKey 	;-- see if we have a number
 		jsr 	IsDigit 	; check if CHRGOT is a digit
 		BR_F(PNotDigit)
 		;-- digit, so collect as number
 		jsr 	P_Number 	; parse number and tokenise it
 		bra 	P_Part 		; loop for more

PNotDigit 	;- is it a quoted string?
 		lda 	CHRGOT
 		BR_ANE('"',PNotStr) 	; A<>" -> PNotStr
 		;-- do parse for [Str] xxxxxx 00
 		jsr 	P_String
 		bra 	P_PartNxt 	; more

PNotStr 	; not string, try varname
 		jsr 	IsAlpha
 		BR_F(PNotVar)
 		jsr 	P_Var
 		bra 	P_Part 		; more

PNotVar 	; not varname, so copy as-is
 		lda 	CHRGOT
 		jsr 	ChrWr
 		jsr 	GetChr
 		bra 	P_Part

;----------------------------------------------- ParseClr
ParseClr 	ldy 	#0
 		lda 	#$EE
ParseClrL 	sta 	TOKBUF,y
 		iny
 		cpy 	#BUFSZ
 		bne 	ParseClrL
 		rts

;----------------------------------------------- BasTop
BasTop: ; Setup to read from start of basic, for now TOKBUF
; for now, just TOKBUF
 		;-- set ZRd to point to first line of Program
 		ZSET(ZRd,TOKBUF-1)
 		;-- set to read from memory
 		PR_RD_MEM() 		; read from memory (code space)
 		PR_WR_IO() 		; output to io (screen)
 		rts
;----------------------------------------------- SkipBlankL
; reads next character nad then checks for balnks
SkipBlankL 	; read next
 		jsr 	GetChr 		; get from io or mem
;----------------------------------------------- SkipBlank
; returns with ending chr in A, (C=1 if A==null)
SkipBlank: ; Skip blank/tabs, end char in A, T if not null
 		;-- null -> exit flase
 		lda 	CHRGOT
 		beq 	RtnFalse
 		;-- blank
 		BR_AEQ(SPC,SkipBlankL) 	; Space -> Loop
 		BR_AEQ(HT,SkipBlankL) 	; TAB   -> loop
 		;-- ok
RtnTrue 	RTN_T()
RtnFalse 	RTN_F()


;----------------------------------------------- IsAlpha
IsAlpha: ; returns T/F if .A is a Alpha A-Z
 		BR_ALT('A',RtnFalse)
 		BR_AGE('Z'+1,RtnFalse)
 		RTN_T()
;----------------------------------------------- IsAlphaNumeric
IsAlphaNumeric: ; returns true if .A is 0-9,A-Z
 		jsr 	IsAlpha
 		BR_T(RtnTrue)
 		;-- fall through to check for digit
 		;-- IsDigit
IsDigit: ; returns T/F if .A is a digit 0..9
 		BR_ALT('0',RtnFalse)
 		BR_AGE('9'+1,RtnFalse)
 		RTN_T()


;----------------------------------------------- IsRelOp
IsRelOp: ; returns T/F if CHRGOT is a relational opperator <>=
 		lda 	CHRGOT
 		BR_AEQ('<',RtnTrue) 	; 3c
 		BR_AEQ('=',RtnTrue) 	; 3d
 		BR_AEQ('>',RtnTrue) 	; 3e
 		RTN_F()


;----------------------------------------------- OneOf
; OneOf: ; see if CHRGOT is one of char in ChkList starting at X
; 		lda 	CHRGOT
; 		beq 	OneOfNo 	; null -> exit F
; OneOfL 		lda 	ChkList,X
; 		beq 	OneOfNo
; 		cmp 	CHRGOT
; 		beq 	OneOfYes
; 		inx
; 		bne 	OneOfL
; OneOfNo 	lda 	CHRGOT
; 		RTN_F() 		; NOPE, set carry & exit
; OneOfYes 	lda 	CHRGOT
; 		RTN_T() 		; YES, clear carry & exit

;----------------------------------------------- P_IsToken
P_IsToken: ; Parse for a token, returns TOKNO or CHRGOT, returns found T/F
; If Found, (T)
; 	returns with ZRd pointing at the last matching char
;       so calling GetChr will fetch the character immediately after the token
; If Not Found, (F)
;       returns with ZRd pointing at the first character of the src word


;----------------------------------------------- TokFind
; TblKeyword	has list of keywords, ending char has $80
; ZRd		points to input, 1st char to try and match Y=0..
; TOKNO		Counts up token number while we search
TokFind: ;try to find a matching token
 		;-- sart with first token no
 		lda 	#0
 		sta 	TOKNO
 		;-- start index in keyword list
 		ldx 	#$FF
TokWord 	;- test next word
 		ldy 	#0
 		inx
TokWordL 	;-- test a character of the word
 		lda 	TblKeyword,X
 		beq 	TokFail 	; end of Token list
 		bmi 	TokLast 	; bit7=1 marks end
 		;--
 		cmp 	(ZRd),Y 	; source char
 		bne 	TokNxt
 		;-- next char
 		iny
 		inx
 		bra 	TokWordL
TokLast 	; last character fo keyword
 		and 	#$7F
 		cmp 	(ZRd),Y
 		beq 	TokFound 	; match so we got it!
 		;-- no match, so next word
TokNxt 		; no match
 		inc 	TOKNO
TokNxtL 	lda 	TblKeyword,X
 		beq 	TokFail
 		bmi 	TokWord
 		inx
 		bra 	TokNxtL
TokFail 	;-- not found, return with CY=1
 		lda 	CHRGOT
 		RTN_F()
TokFound 	;-- found !
 		clc 			; move ZRd to after the keyword
 		tya 			; ZRd = ZRd + Y
 		adc 	ZRd
 		sta 	ZRd
 		lda 	#0
 		adc 	ZRd+1
 		sta 	ZRd+1
 		;-- get token number from the counter
TokRtn 		lda 	TOKNO
 		asl 	A 		; token nos incr in 2s
 		ora 	#$80 		; and have high bit set
 		;-- return Found=True, A=token
 		RTN_T()

;----------------------------------------------- TokExpand
TokExpand: ; Expand the token to key word (X is index)
 		ldy 	#0
 		txa
 		and 	#$7F
 		sta 	TOKNO
 		lsr 	a 		; A = TokNo / 2
 		tax 			; index in table
 		cpx 	#0
 		beq 	TokExpL0 	; token 80 is special ;P
 		dey

 		;--
TokExpNxt 	cpx 	#0 		; no more
 		beq 	TokExpX
 		;-- move to next word
TokExpNxtL 	iny
 		lda 	TblKeyword,y
 		beq 	TokExpXX 	; end of list, not found
 		bpl 	TokExpNxtL
 		;-- got the marker
 		dex
 		bne 	TokExpNxtL
TokExpL 	;-- found the start of the word
 		iny 			; skip over the last end-of-word marker
TokExpL0 	;-- output the word
 		lda 	TblKeyword,y
 		beq 	TokExpX 	; end of list -> done
 		bmi 	TokExpLX 	; last char
 		jsr 	PutChr 		; echo
 		bra 	TokExpL 	; loop next char

TokExpLX 	;-- last character of the word (marked one)
 		and 	#$7F 		; mask off bit7
 		jsr 	PutChr 		; output it and return
 		cmp 	#'('
 		beq 	TokExpX
 		bra 	TokSpc

TokExpXX 	; not found
 		lda 	#'['
 		jsr 	PutChr
 		lda 	TOKNO
 		jsr 	PrAHex
 		lda 	#']'
 		jsr 	PutChr
TokSpc 		lda 	#' '
 		jsr 	PutChr
TokExpX 	rts

;----------------------------------------------- Run
Run: ; Run from start
 		jsr 	BasTop
RunL 		jsr 	GetChr 		; (++ZRd)->A
 		beq 	RunX 		; null = End-of-program
 		jsr 	RunLnX
 		bra 	RunL

RunX 		rts 			; done, return


RunLn: ; Run line pointed to by ZRd
 		jsr 	GetChr 		; (++ZRd)->A
 		bne 	RunLnX 		; null = End-of-line
 		rts 			; done, return

RunLnX 		jsr 	RunTok 		; (*2)
 		bra 	RunLn

RunTok: ; run token
 		lda 	CHRGOT
 		CASE(T_Line,X_LineNo) 	; if A==T_Line goto X_LineNo
 		;-- otherwise token?
 		BR_AGE($80,RunJmp)
 		;-- not token, treat as ASCII
 		jsr 	PutChr
 		rts

RunJmp: ; run via jump table, return to (*2)
 		and 	#$7F
 		tax
 		jmp 	(TblRun,X)


;----------------------------------------------- List Scrn
List: ; List from start of program
 		;-- from start
 		jsr 	BasTop
 		;-- list to output device IO
List1 		PR_WR_IO()
 		;-- fall through to list line(s)

;----------------------------------------------- ListLn
ListLn: ; List line pointed to by ZRd
 		jsr 	GetChr 		; (++ZRd)->A
 		beq 	ListLnX 	; null = done
 		jsr 	ListTok 	; Expand		(*1)
 		bra 	ListLn
ListLnX 	jmp 	PrNL 		; NL and return

ListTok 	; detokenise A=CHRGOT
 		CASE(T_Line,L_LineNo) 	;->L_LINEno, rts to (*1)
 		CASE(T_Number,L_Number)
 		CASE(T_String,L_String)
 		CASE(T_Var,L_Var)
 		CASE(T_VarName,L_VarName)
; 		;-- otherwise token?
 		tax
 		BR_ALT($80,ListTokErr)
 		;-- expand the token
 		jsr 	TokExpand
 		rts
ListTokErr 	;-- not token, treat as ASCII
 		jsr 	PutChr
 		rts

.export L_LineNo

;----------------------------------------------- NextLine
NextLine: ; point ZRd to the next line NXTLN
 		;-- point ZRd to the start of the lilne
 		lda 	NXTLN
 		sta 	ZRd
 		lda 	NXTLN+1
 		sta 	ZRd+1
 		rts

;=================================================================== VARIABLE =========
P_Var: ; Check for variable name
; ZRd points at the initial alpha char
;                7654.3210
; Variable ID =  0($n.nnnn nnnnn=1-26 for A-Z
 		;-- store Alpha so long
 		and 	#$1F 		; mask to index 0-31 @,A-Z,...
 		sta 	VARID
 		;-- if next char isAlphaNum then long name
 		jsr 	ChrRd 		; get next char
 		jsr 	IsAlphaNumeric
 		BR_T(P_VarName) 	; A_Z,0-9 -> long name
 		;-- single char var name - build up the id
 		;-- start by shifting VARID - nnnnn
 		;-- now check for $
 		BR_ANE('$', P_Var1)
 		jsr 	ChrRd 		; consume the '$'
 		SMB5 	VARID 		; set bit 5='$'
P_Var1 		;-- check for array '('
 		BR_ANE('(', P_Var2)
 		jsr 	ChrRd 		; consume the '('
 		SMB6 	VARID 		; set bit 6='('
P_Var2 		;-- now output token
 		lda 	#T_Var
 		jsr 	ChrWr
 		;-- output VARID
 		lda 	VARID
 		jmp 	ChrWr 		; write and return

;----------------------------------------------- P_VarName
P_VarName: ; long variable name
; TODO - for now output token and string of chars with null
 		lda 	#T_VarName 	; token
 		jsr 	ChrWr
 		lda 	VARID
 		ora 	#$40
 		jsr 	ChrWr 		; 1st Alpha
 		lda 	CHRGOT
P_VarNameL 	jsr 	ChrWr 		; 2nd char
 		jsr 	ChrRd 		; get char
 		jsr 	IsAlphaNumeric
 		BR_T(P_VarNameL) 	;copy to line
 		lda 	#0
 		jsr 	ChrWr
 		rts

;----------------------------------------------- L_Var
L_Var: ; list 'simple' variable
 		jsr 	ChrRd 		; get the var id
 		sta 	VARID
 		and 	#$1f
 		ora 	#$40
 		jsr 	PutChr 		; output the alpha
L_Var1 		;- handle the flags
 		BBR5 	VARID,L_Var2 	; '$' ?
 		lda 	#'$'
 		jsr 	PutChr
L_Var2 		BBR6 	VARID,L_Var3 	; '(' ?
 		lda 	#'('
 		jsr 	PutChr

L_Var3 		rts 			;//jmp 	ChrRd 		; read next and return

;                7654.3210
; Variable ID =  0($n.nnnn nnnnn=1-26 for A-Z

;----------------------------------------------- L_VarName
L_VarName: ; list long variable
 		jmp 	ChrCopy

;=================================================================== NUMBER ===========

;----------------------------------------------- P_Number
P_Number: ; parse a number into token & value
; for now were only doing 16bit int numbers
 		;-- Write Number token
 		lda 	#T_Number
 		jsr 	ChrWr
 		;-- Parse number value
 		jsr 	RdNum
; 		ZDEC(ZRd)
 		;-- write number value
 		lda 	NUMBER
 		jsr 	ChrWr
 		lda 	NUMBER+1
 		jsr 	ChrWr
 		;--
 		rts
;----------------------------------------------- L_Number
L_Number: ; list number
 		;-- get number from code -> NUMBER
 		jsr 	FetchNum
 		;-- convert & output number
 		jsr 	PrNum
; 		lda 	#' '
; 		jsr 	PutChr
 		;--
 		rts
;----------------------------------------------- FetchNum
FetchNum: ; fetch number from the code -> NUMBER -> AX
 		lda 	#0
 		sta 	NUMBER+2
 		jsr 	ChrRd
 		sta 	NUMBER
 		jsr 	ChrRd
 		sta 	NUMBER+1
 		rts

;=================================================================== STRING ===========

;----------------------------------------------- P_String
P_String: ; parse quoted string into token
 		;-- Write Number token
 		lda 	#T_String
 		jsr 	ChrWr
 		;-- Copy until the closing quote
P_StrL 		jsr 	ChrRd
 		beq 	P_StrX 		; null -> done
 		BR_AEQ('"',P_StrX) 	; quote -> done
 		jsr 	ChrWr 		; copy to TOKBUF
 		bra 	P_StrL 		; loop back for more
P_StrX 		lda 	#0
 		jsr 	ChrWr 		; place temrinating null and return
 		rts

;----------------------------------------------- L_String
L_String: ; list out string text
 		lda 	#'"'
 		jsr 	PutChr
 		jsr 	ChrCopy
 		lda 	#'"'
 		jsr 	PutChr
 		rts

;=================================================================== LINE_NO ==========

;----------------------------------------------- P_LineNo
P_LineNo: ; Parse line numebr and place int TOKBUF
 		;-- Write LineNo token
 		lda 	#T_Line
 		jsr 	ChrWr
 		;-- Parse number value
 		jsr 	RdNum
 		;-- write line number
 		lda 	NUMBER
 		jsr 	ChrWr
 		lda 	NUMBER+1
 		jsr 	ChrWr
 		;-- place holder for the length
 		lda 	#$AA
 		jsr 	ChrWr
 		rts

;----------------------------------------------- X_LineNo
X_LineNo: ; Get LNNUM & Set up NXTLN
; enters with A=CHRGOT= T_Line
 		;-- save line no
 		jsr 	ChrRd 		; get lo no
 		sta 	LNNUM
 		jsr 	ChrRd 		; hi num
 		sta 	LNNUM+1
 		;-- get length
 		jsr 	ChrRd
 		;-- ZRd points to the length byte
 		;-- Next call to ChrRd will return the byte after this token
 		clc
 		adc 	ZRd
 		sta 	NXTLN
 		lda 	#0
 		adc 	ZRd+1
 		sta 	NXTLN+1
 		;--
X_LineNoX 	rts

;----------------------------------------------- L_LineNo
L_LineNo: ; list LineNo
; ChrRd will get first byte afer the token
 		jsr 	X_LineNo 	; Get LNNUM & Set up NXTLN
 		;-- output line no
 		lda 	LNNUM
 		sta 	NUMBER
 		lda 	LNNUM+1
 		sta 	NUMBER+1
 		lda 	#0
 		sta 	NUMBER+2 	; U=0
 		;--
 		jsr 	PrNum
 		lda 	#' '
 		jsr 	PutChr
 		;-- jump to continue listing
 		rts

.export T_String,T_Line,T_Number

.export TokFind,TOKNO,TokFound,Parse

.export TblKeyword,TblRun


.echo "  "
.echo $-_Tok
.echo " Bytes in _Tok\n"

